options {
    STATIC = false;
    DEBUG_PARSER = true;
}

PARSER_BEGIN(Parser)

public class Parser {

}
PARSER_END(Parser)

// Lexer
TOKEN : {
    "+" | "-" | "*" | "/" | "%" | "++" | "--" | "&&" | "||" | "|" | "!" | "^"| "=" | "," | "."
    | "[" | "]" | "[]" | "[[" | "]]" | "(" | ")" | "<" | ">" | "{" | "}" | ";" | "$"
    | "if" | "else" | "while" | "return"
    | <Type : "Range" | "State" | "Transition" | "FA" | "RA" |"int" | "double" | "char" | "boolean">
    | "Set" | "Map"
    | <BoolLiteral : "true" | "false">
    | <CharLiteral : "'" ["A"-"Z", "a"-"z", "?", "!"] "'">
    | <StringLiteral : "\"" ["A"-"Z", "a"-"z", "_"](["A"-"Z", "a"-"z", "_", "0"-"9"])* "\"">
    | <IntegerLiteral : "0" | (["1"-"9"] (["0"-"9"])*)>
    | <RegexLiteral : "/" (<StringLiteral> | <CharLiteral> | "(" | ")" | "[" | "]" | "+" | "*" | "|")+ "/" >
    | <Identifier : ["A"-"Z", "a"-"z", "_"](["A"-"Z", "a"-"z", "_", "0" - "9"])*>
}

SKIP :
{
    // White space
    " "
    | "\t"
    | "\n"
    | "\r"
    // Single Line Comment
    | <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
    // Multi Line Comment
    | <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

// Parser

// CU -> (decl | stmnt)*
void cu(): {} {
    (decl() | stmnt())*
}

// decl -> <Type> <Identifier> (varDecl | methDecl | ";")
void decl(): {} {
    <Type> <Identifier> (varDecl() | methDecl() | ";")
}

// varDecl -> "=" atom ";"
void varDecl(): {} {
    "=" expr() ";"
}

// methDecl -> "(" (<Type> <Identifier> ("," <Type> <Identifier>)*)? ")" stmnt
void methDecl(): {} {
    "(" (<Type> <Identifier> ("," <Type> <Identifier>)*)? ")" stmnt()
}

// stmnt -> returnStmnt | ifStmnt | whileStmnt | blockStmnt | expr | ";"
void stmnt(): {} {
    returnStmnt() | ifStmnt() | whileStmnt() | blockStmnt() | expr() | ";"
}

// returnStmnt -> "return" atom ";"
void returnStmnt(): {} {
    "return" expr() ";"
}

// ifStmnt -> "if" "(" atom ")" stmnt ("else" stmnt)?
void ifStmnt(): {} {
    "if" "("  expr() ")" stmnt() (LOOKAHEAD(1)"else" stmnt())?
}

// whileStmnt -> "while" "(" atom ")" stmnt
void whileStmnt(): {} {
    "while" "(" expr() ")" stmnt()
}

// blockStmnt -> "{" (decl | stmnt)* "}"
void blockStmnt(): {} {
    "{" (decl() | stmnt())* "}"
}

// atom -> "!" atom | ("+" | "-") atom | <Character> | <String> | <Integer> | <Boolean> | <Identifier>
//         | expr | range | state | transition | fa | methCall
void atom(): {} {
    "!" atom() | <CharLiteral> | <StringLiteral> | <IntegerLiteral> | <BoolLiteral>
    | range() | fa() | LOOKAHEAD(2) methCall()
    | (state() | <Identifier> ("." <Identifier>)*) (LOOKAHEAD(3) transition())?
}

// methCall -> (<Idenfier> ".")? <Identifier> "(" (atom ("," atom)*)? ")"
void methCall(): {} {
    <Identifier> "(" (atom() ("," atom())*)? ")"
}

// range -> "[" <Char> ("-" <Char)? ("," <Char> ("-" <Char)?)* "]"
void range(): {} {
    "[" <CharLiteral> ("-" <CharLiteral>)? ("," <CharLiteral> ("-" <CharLiteral> )?)* "]"
    (LOOKAHEAD(2) "+" (range() | <Identifier>))*
}

// state -> "$" <String> ("^" (<Integer> | <Identifier>))?
void state(): {} {
    "$" <StringLiteral> ("^" (<IntegerLiteral> | <Identifier>))?
}

// transition -> (state | <Identifier>) (("--" (range | <Identifier)) | "-") "-->" (state | <Identifier>)
void transition(): {} {
    (("--" (range() | <Identifier>)) | "-") "-->" (state() | <Identifier>)
}

// fa -> "<" (state | <Identifier>) ",{" (transition ("," transition)*)? "}>"
void fa(): {} {
    "<" (state() | <Identifier>) "," "{" (<Identifier>(transition())? ("," <Identifier>(transition())?)?)? "}" ">"
    (LOOKAHEAD(2) "+" (fa() | <Identifier>))*
}

// expr -> "(" expr ")" | compExpr (("&&" | "||") compExpr)*
void expr(): {} {
    ("(" expr() ")") | (compExpr() (("&&" | "||") compExpr())*)
}

// compExpr -> sumExpr (("=="|"!="|"<="|">="|"<"|">") sumExpr)*
void compExpr(): {} {
    sumExpr() (LOOKAHEAD(2)("=="|"!="|"<="|">="|"<"|">") sumExpr())*
}

// sumExpr -> prodExpr (("+" | "-") prodExpr)*
void sumExpr(): {} {
    prodExpr() (("+"|"-") prodExpr())*
}

// prodExpr -> atom (("*" | "/") atom)*
void prodExpr(): {} {
    atom() (("*"|"/") atom())*
}
